
-> Start

=== Continue ===
#news
+ [\/\/ {~Continue|Next|Advance|Proceed|Send|Transmit|Unveil|Forthwith|More|Ready|Validate|Go on.|Carry on.|Just so.|Hence...|By your leave...|Lest...|Onward...|Whence...|Pause...|So then...|So...|Also...|When...|And...|Then...|And so...|And then...}]
- ->->

=== Silence ===
#news
+ [\/\/ {~Nothing|Silence|Where are you?|Why won't you respond?|Into the brink.|Blackness|Unbearable|Anon...}]
- ->->

=== Telegram(d) ===
#style:telegram
#from:Deirdre MacKenna
#to:Colm O'Brien
~ date = d
{date} $header
->->

=== TelegramNPC(d) ===
#style:telegram-npc
#from:Colm O'Brien
#to:Deirdre MacKenna
~ date = d
{date} $header
->->

=== Start ===

#left:
#right:

VAR date = ""

#style:news
Telegraph #news-title
#style:news-subtitle
by
Cassandra Gray
Brian Dean Jennings
Michael McDonald

-> Continue ->

1938 #news-title

#left:Player
#style:write
#space

Dearest Dairy,
+ [I've received a letter from London today! Could it be from my sweet Colm?]


#right:Colm
#style:write-npc

- 4th December 1938`<>
From: Colm O'Brien`<>
London, England`

To: Deirdre MacKenna`<>
Belfast, Northern Ireland

+ [Joy! A letter from Colm. Always a pleasure. $write]
+ [Oh no. A letter from Colm, no less. What has happened? $write]

- Dearest Deirdre, mo mhuirnin.

It has been nearly five years since we last met, yet I can still hear the enchanting sound of your voice in my ears.
As I draw nearer to completing my apprenticeship I've begun counting the days until I may return to Belfast, and to your side.
Your last letter brought much delight to everyone, they delight in hearing tales of the misadventures of your students and agree that you must be the most understanding of teachers to put up with them!

+ [He's read my letters, then? Aye, it warms the heart. $write]
+ [Sharing my intimacies with the boys? How embarrassing. $write]

- Work here has been neverending, which is useful for occupying my mind during the day and ensuring I fall quickly to sleep in the evening.
I must confess that it's not just thoughts of missing you occupying my mind and keeping me awake.
Germany is only becoming more aggressive, and since September I've been hearing whispers in the street that England is preparing for war.

Please... don't say what I think you are about to say. $write
+ [I can barely bring myself to read on. $write]

- I've given it all the thought I can spare, and though as an Irishman I will not be conscripted for war I feel it's my duty to volunteer for service in the British Army.
I know what you will say, that we've been separated for too long already, that I need not risk my life, but I know I must do this. I've spent nigh on five years in England, and during this time it has been my home.

+ [Separated too long and then some. How long must we wait? $write]

- Please forgive me mo mhuirnin dilis, a chuisle mo chroi.

I await your reply.

Always and forever yours,`<>
Colm

#style:write

Preparing for war? What on earth is he thinking?!`<>
Oh, Colm. You poor fool.

- -> Telegram("16 Dec. 1938") ->
+ [Are you mad STOP]

- -> TelegramNPC("17 Dec. 1938") -> 
Is my duty STOP`<>
Please forgive my making you wait for me

- -> Telegram("19 Dec. 1938") ->
+ [Would wait 10 years STOP`Worried for your life]

    -> TelegramNPC("20 Dec. 1938") -> 
    Do not fret love STOP`<>
    My friends and I will protect each other

+ [You must not volunteer moi chroi]

    -> TelegramNPC("20 Dec. 1938") -> 
    My family has a tradition of serving STOP`Following in my fathers and his fathers footsteps

- -> test

=== test ===
- + [Something more convincing then...? $write]

- -> Telegram("22 Dec. 1938") ->

+ [Don't volunteer STOP I'll leave you]

    -> TelegramNPC("25 Dec. 1938") -> 
    Love if I thought that true I would be back tomorrow STOP`<>

+ [You mustn't volunteer STOP`<>I'm pregnant]

    -> TelegramNPC("25 Dec. 1938") -> 
    We've not seen each other since 1933 STOP`<>
    Give my congratulations to the father STOP`<>

+ [I understand love STOP`I pray daily that it won't come to war]

    -> TelegramNPC("25 Dec. 1938") -> 
    I knew you would understand STOP`<>

- Merry Christmas Mo Chroi

-> Continue ->

-> Telegram("27 Dec. 1938") ->
Merry Christmas mo mhuirnin

-> Continue ->

1939 #news-title

-> Continue ->

-> TelegramNPC("02 Sept. 1939") -> 
Have just received orders sending us to France STOP`<>
Will write when I can

-> Telegram("03 Sept. 1939") -> 
+ [Can they drop you off in Belfast on the way]

    -> TelegramNPC("29 Sept. 1939") -> 
    Asked but alas they were not taking requests

+ [Watch out for the French girls]

    -> TelegramNPC("29 Sept. 1939") -> 
    Alas but I already took a wife I think you would like her

- -> Telegram("14 Oct. 1939") -> 
+ [Glad you keep your sense of humour]

- -> TelegramNPC("16 Oct. 1939") -> 
Always mo chroi

-> Continue ->

- 1940 #news-title
- The war intensifies. #style:news-subtitle

#style:news
Colm and Dee exchange letters and telegrams through the year, Colm telling stories of his fellow soldiers and his time in France and Dee regaling them all with stories of her students, and life in Belfast.
Together they plan for the future and Colm's return from the war, both refusing to acknowledge that it may never happen.

-> Continue ->

-> TelegramNPC("23 Dec. 1940") -> 
My heart is heavy at the thought of another Christmas away mo chroi

-> Telegram("24 Dec. 1940") -> 
+ [Pray my last letter reach you before Christmas, that it may give you comfort]

    -> TelegramNPC("30 Dec. 1940") -> 
    It reached me just prior to your last telegram STOP`<>
    It was a ray of sun on the cloudiest day

+ [Surely this must all be over soon mhuirnin STOP I can scarcely bear it]

    -> TelegramNPC("30 Dec. 1940") -> 
    I pray for a swift end to the war often but I do not trust a hope

- -> Continue ->

1940 #news-title

-> Continue ->

-> TelegramNPC("09 May. 1941") -> 
Received word of raid in Belfast praying for news of your safety mo mhuirnin

-> Telegram("10 May. 1940") -> 
+ [Am safe STOP`Bombs dropped elsewhere STOP`Will write soon]

- -> Continue ->

1941 #news-title

-> Continue ->

#style:write

11th April 1941``<>

From: Deirdre MacKenna`<>
Belfast, Northern Ireland

To: Colm O'Brien`<>
Cyrenaica, Libya

-> Continue ->

My Dearest, Mo mhuirnin dilis,

I don't have much time to write, I just wanted to assure you I am safe and well for now.
I took shelter under the stairs, but the bombs fell on the other side of the city and caused minimal damage.
The thought of further attacks presses on me but I've taken to distracting myself by sewing my wedding dress and imagining what our lives will be one day.

-> Continue ->

Easter nears soon, and I'm planning to attend the church service with Mam and Da before Easter Sunday lunch.
I've bought a new hat for the occasion and I must say it rather brings out my eyes and makes me feel quite fashionable.
I will write more soon Mo Stor, write back when you can.

-> Continue ->

Love always and forever,`<>
Dee

-> Continue ->

-> TelegramNPC("16 April 1941") -> 
Received word of massive air raid in Belfast please send word of your safety mo chroi

-> Silence ->

-> TelegramNPC("17 April 1941") -> 
Have not received word from you love please send as soon as able

-> Silence ->

-> TelegramNPC("18 April 1941") -> 
I pray every day for word that you're alive my dear

-> Silence ->

-> TelegramNPC("19 April 1941") -> 
I am beginning to lose hope that I will ever hear from you again

-> Silence ->

-> Telegram("20 April 1941") -> 
+ [Sorry for delay am safe ran to church shelter will write more soon]

#style:write

- 22nd April 1941`<>

From: Deirdre MacKenna`<>
Belfast, Northern Ireland

To: Colm O'Brien`<>
Cyrenaica, Libya

-> Continue ->

Colm,

Forgive the delay of my letter but in the aftermath of the bombings it has been difficult to find the time, and the means to send mail. As I said in my telegram I am safe, though I confess myself rather shaken.
The blitz last Tuesday was the second raid in as many weeks, and they said more are expected. I have heard in town that several young men at the football match spotted a lone Luftwaffe plane circling hours earlier.
We had almost no warning, I had scarecely lain down to sleep when I heard the air raid siren begin. I roused myself as fast as I could and ran for the air raid shelter at Clonard Monastery.

-> Continue ->

The shelter was located in the crypt underneath the sanctuary, and I found myself wondering if all we had done by gathering there was save the city the cost of moving our bodies after the raid was over.
There were at least two hundred of us huddled in there, and it was a sombre mood to be sure. To keep the little ones calm and drown out the sound of bombs falling we said prayers and sung hymns.
Finally when the air had been silent for a good while we ventured out to see what was left. It seemed half the city was on fire. People have been fleeing the city ever since, making for Dromara, Newtownards, Lisburn and even Dublin.

-> Continue ->

I wish you were here with me. I will remain strong, have faith and look forward to the day we are united once more.``<>

Deirdre MacKenna

-> Continue ->

-> TelegramNPC("23 April 1941") -> 
Words cannot express the relief felt to receive word from you dear

-> Continue ->

-> Telegram("24 April 1941") -> 
+ [Further raids prevent me from writing too often STOP`You are in my thoughts]

    -> TelegramNPC("29 April 1941") -> 
    I understand STOP`<>
    I have all your past letters to sustain me between telegrams

+ [Think it best we stick to telegrams from now mail system unreliable after raids]

    -> TelegramNPC("29 April 1941") -> 
    Whatever you wish my love as long as I know you are safe

- -> Continue ->

-> TelegramNPC("15 May 1941") -> 
Are you well love STOP`<>
No word from you for days

-> Silence ->

-> TelegramNPC("20 May 1941") -> 
We have no word of further bombings but I have no word of you love

-> Silence ->

-> Telegram("27 May 1941") -> 
+ [I am preoccupied with my students STOP`We took in children from destroyed schools]

    -> TelegramNPC("29 May 1941") -> 
    Understood love STOP`<>
    Is easy to forget the outside world exists

+ [It hurts too much knowing you are so far away]

    -> TelegramNPC("28 May 1941") -> 
    Understood love STOP`<>
    I feel the same way STOP`<>
    Stay strong and have faith

#style:news-subtitle
- + [Time Passes]

- -> TelegramNPC("15 July 1941") -> 
mo chroi sorry for lapse in communication STOP`<>
fighting has been intense STOP`<>
I hoped perhaps my last letter might make it to you STOP`<>
perhaps you could send me one

-> Continue ->

-> Telegram("20 July 1941") -> 
+ [I received no letter my love]

    -> TelegramNPC("22 July 1941") -> 
    I fear the mail system must truly be unsalvageable STOP`<>
    It can be summarized simply as I love you and I look to when we can be together again.

+ [I will try and find time to write]

    -> TelegramNPC("22 July 1941") -> 
    Thoughts of your letters keep me going during the worst battles.

- -> Continue ->

1942 #news-title

-> Silence ->

-> TelegramNPC("10 Aug. 1942") -> 
I worry about you Dee STOP`<>
Our correspondence has become so infrequent STOP

-> Silence ->

1943 #news-title

-> Silence ->

-> TelegramNPC("03 May 1943") -> 
Mo chroi have you tired of waiting for me STOP

-> Continue ->

-> Telegram("06 May 1943") -> 
+ [Never darling STOP`I'm just so tired STOP`I will love you the rest of your life]

- 1944 #news-title

-> Silence ->

1945 #news-title

-> Silence ->

-> TelegramNPC("03 Sept. 1945") -> 
It has finally happened my love STOP`<>
We are to return home STOP`<>
Look for me at the Port of Belfast and we can finally begin our life together

-> Continue ->

#left:
#reveal

#style:news
3rd September 1945``<>

From: Alma Fitzgerald`<>
Belfast, Northern Ireland

To: Colm O'Brien`<>
\**URGENT**

-> Continue ->

#style:write

Dearest Colm.

You don't know me, but I feel as though I know you. My name is Alma Fitzgerald, and I'm a telegraphist with the Electric Telegraph Company in Belfast.
I'm also a widower, having lost my husband in the first world war. I tell you this not to ask for sympathy or forgiveness but to perhaps help you understand why I did what I did.
I've been sending and receiving telegrams between dear Deirdre and yourself since your time in London in 1938, and throughout the years I've witnessed the love between you grow and delighted in the stories you'd tell one another. When I heard you were volunteering for the British Army I worried as though you were my own son.

-> Continue ->

I watched in admiration as the both of you handled the further separation and anxieties of war with maturity and love, until one day I received a telegram for you bearing the worst.

-> Continue ->

That Deirdre had perished in the Easter Tuesday air raids by the Luftwaffe.

-> Continue ->

Dear Colm, I'm so sorry, for your loss and for my actions afterwards.

-> Continue ->

Staring at that telegram I felt like I was losing my husband all over again, I could not bring myself to send it. For four days I tried to send it.
I read your telegrams coming in day after day, begging for word from Deirdre and I couldn't bring myself to deliver such a blow, so I sent the telegram on the 20th, pretending to be Deirdre.

-> Continue ->

I don't know why I did this, I have no rational explanation beyond trying to spare you pain for as long as possible. I thought perhaps if you and Deirdre grew apart naturally you would return to London and need never know the truth, but I could never bring myself to truly push you away from her.
I know she truly loved you, and it felt a disservice to her memory to tarnish that.)

-> Continue ->

I don't expect you to forgive me, and I hope that in time you're able to love again, as I feel we both know that's what Deirdre would've wanted.

-> Continue ->

For once sincerely,`<>
Alma Fitzgerald

#style:telegram
+ [END] -> Start
